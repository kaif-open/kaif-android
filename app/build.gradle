apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

  Properties versionProps = new Properties()
  versionProps.load(new FileInputStream(file('version.properties')))
  def buildVersionCode = versionProps['VERSION_CODE']
  def buildVersionName = versionProps['VERSION_NAME_PREFIX'] + "." + buildVersionCode

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  compileSdkVersion 26
  buildToolsVersion rootProject.buildToolsVersion

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }

  defaultConfig {
    applicationId "io.kaif.mobile"
    minSdkVersion 16
    targetSdkVersion 26
    versionCode buildVersionCode.toInteger()
    versionName buildVersionName
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    Properties props = new Properties()
    props.load(new FileInputStream(file('../secret/password.properties')))

    debug {
      storeFile file("../secret/kaif-keystore.jks")
      storePassword props["KEY_STORE_PASSWORD"]
    }
    release {
      storeFile file("../secret/kaif-keystore.jks")
      storePassword props["KEY_STORE_PASSWORD"]
      keyAlias "ReleaseKey"
      keyPassword props["RELEASE_KEY_PASSWORD"]
    }
  }

  buildTypes {
    Properties props = new Properties()
    props.load(new FileInputStream(file('../secret/api.properties')))

    debug {
      signingConfig signingConfigs.debug
      buildConfigField 'String', 'CLIENT_ID', "\"" + props["CLIENT_ID"] + "\""
      buildConfigField 'String', 'CLIENT_SECRET', "\"" + props["CLIENT_SECRET"] + "\""
    }
    release {
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      buildConfigField 'String', 'CLIENT_ID', "\"" + props["CLIENT_ID"] + "\""
      buildConfigField 'String', 'CLIENT_SECRET', "\"" + props["CLIENT_SECRET"] + "\""
    }
  }

  lintOptions {
    disable 'InvalidPackage'
    abortOnError false
  }

  testOptions.unitTests.all {
    testLogging {
      events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  kapt "com.google.dagger:dagger-compiler:" + rootProject.daggerVersion
  kapt 'com.jakewharton:butterknife-compiler:' + rootProject.butterknifeVersion
  kapt project(':retry-stale-processor')
  provided 'javax.annotation:jsr250-api:1.0'
  implementation 'com.android.support:appcompat-v7:' + rootProject.supportLibVersion
  implementation 'com.android.support:recyclerview-v7:' + rootProject.supportLibVersion
  implementation 'com.android.support:design:' + rootProject.supportLibVersion
  implementation 'com.android.support:support-v4:' + rootProject.supportLibVersion
  implementation 'io.reactivex:rxandroid:1.2.1'
  implementation 'com.jakewharton.rxbinding:rxbinding:0.4.0'
  implementation 'com.jakewharton.rxbinding:rxbinding-design:0.4.0'
  implementation 'com.trello:rxlifecycle:1.0'
  implementation 'com.trello:rxlifecycle-components:1.0'
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:' + rootProject.kotlinVersion
  implementation 'org.jetbrains.anko:anko-sdk25:' + rootProject.ankoVersion
  implementation 'org.jetbrains.anko:anko-recyclerview-v7:' + rootProject.ankoVersion
  implementation 'org.jetbrains.anko:anko-design:' + rootProject.ankoVersion
  implementation 'org.jetbrains.anko:anko-support-v4:' + rootProject.ankoVersion
  implementation 'org.jetbrains.anko:anko-appcompat-v7:' + rootProject.ankoVersion
  implementation 'com.jakewharton:butterknife:' + rootProject.butterknifeVersion
  implementation 'com.squareup.retrofit2:retrofit:' + rootProject.retrofitVersion
  implementation 'com.squareup.retrofit2:converter-gson:' + rootProject.retrofitVersion
  implementation 'com.squareup.retrofit2:adapter-rxjava:' + rootProject.retrofitVersion
  implementation 'com.google.dagger:dagger:' + rootProject.daggerVersion
  implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
  implementation 'org.apache.commons:commons-lang3:3.7'
  debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
  androidTestImplementation 'org.mockito:mockito-core:2.13.0'
  androidTestImplementation 'com.google.dexmaker:dexmaker:1.0'
  androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.0'
  androidTestImplementation 'com.android.support:support-annotations:' + rootProject.supportLibVersion
  androidTestImplementation 'com.android.support.test:runner: ' + rootProject.runnerVersion
  androidTestImplementation 'com.android.support.test:rules:' + rootProject.rulesVersion
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:' + rootProject.espressoVersion
  androidTestImplementation 'com.android.support.test.espresso:espresso-intents:' + rootProject.espressoVersion
  androidTestImplementation 'com.google.dagger:dagger:' + rootProject.daggerVersion
  kaptAndroidTest 'com.google.dagger:dagger-compiler:' + rootProject.daggerVersion
}

task releaseNewVersion(dependsOn: 'assembleRelease') doLast {
  Properties versionProps = new Properties()

  def file = file('version.properties')
  versionProps.load(new FileInputStream(file))

  def nextCode = versionProps['VERSION_CODE'].toInteger() + 1
  versionProps['VERSION_CODE'] = nextCode.toString()
  versionProps.store(file.newWriter(), null)
}

repositories {
  mavenCentral()
}
